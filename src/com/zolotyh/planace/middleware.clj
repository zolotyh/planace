(ns com.zolotyh.planace.middleware
  (:require
   [com.biffweb :as biff]
   [com.zolotyh.planace.poker.path-ids :as paths-ids]
   [muuntaja.middleware :as muuntaja]
   [reitit.core :as r]
   [ring.middleware.anti-forgery :as csrf]
   [ring.middleware.defaults :as rd]))

(defn wrap-redirect-signed-in [handler]
  (fn [{:keys [session reitit.core/router] :as ctx}]
    (if (some? (:uid session))
      (let [url (:path
                 (r/match-by-name router paths-ids/room-list))]
        {:status 303
         :headers {"location" url}})
      (handler ctx))))

(defn wrap-signed-in [handler]
  (fn [{:keys [session] :as ctx}]
    (if (some? (:uid session))
      (handler ctx)
      {:status 303
       :headers {"location" "/signin?error=not-signed-in"}})))

;; Stick this function somewhere in your middleware stack below if you want to
;; inspect what things look like before/after certain middleware fns run.
(defn wrap-debug [handler]
  (fn [ctx]
    (let [response (handler ctx)]
      (println "REQUEST")
      (biff/pprint ctx)
      #_{:clj-kondo/ignore [:inline-def]}
      (def ctx* ctx)
      (println "RESPONSE")
      (biff/pprint response)
      #_{:clj-kondo/ignore [:inline-def]}
      (def response* response)
      response)))

(defn wrap-site-defaults [handler]
  (-> handler
      biff/wrap-render-rum
      biff/wrap-anti-forgery-websockets
      csrf/wrap-anti-forgery
      biff/wrap-session
      muuntaja/wrap-params
      muuntaja/wrap-format
      (rd/wrap-defaults (-> rd/site-defaults
                            (assoc-in [:security :anti-forgery] false)
                            (assoc-in [:responses :absolute-redirects] true)
                            (assoc :session false)
                            (assoc :static false)))))

(defn wrap-api-defaults [handler]
  (-> handler
      muuntaja/wrap-params
      muuntaja/wrap-format
      (rd/wrap-defaults rd/api-defaults)))

(defn wrap-base-defaults [handler]
  (-> handler
      biff/wrap-https-scheme
      biff/wrap-resource
      biff/wrap-internal-error
      biff/wrap-ssl
      biff/wrap-log-requests))
